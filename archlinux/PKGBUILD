# Maintainer: Salvatore Mesoraca <s.mesoraca16@gmail.com>
pkgname=saractl
pkgver=r50.19575b0
pkgrel=1
pkgdesc="Userspace counterpart of the S.A.R.A. LSM"
arch=('any')
url="https://github.com/smeso/saractl"
license=('GPL3')
depends=('python-setuptools' 'python-pyelftools' 'python-prctl' 'python-pyxattr')
makedepends=('git')
backup=('etc/sara/main.conf'
        'etc/sara/wxprot.conf.d/99_wxprot.conf')
source=('git+https://github.com/smeso/saractl.git')
sha256sums=('SKIP')

pkgver() {
  cd "${srcdir}/${pkgname}"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

package() {
  cd "${srcdir}/${pkgname}"
  python setup.py install --root="${pkgdir}" --optimize=1
  install -Dm644 debian/sara.service "${pkgdir}/usr/lib/systemd/system/sara.service"
}
